;%ifdef CONFIG
;{
;  "RegData": {
;    "RAX": "0x000000000eca8642",
;    "RBX": "0x000000010eca8642",
;    "RCX": "0x000000000eca8642",
;    "RDX": "0x000000010eca8642"
;  },
;  "MemoryRegions": {
;    "0x1000000": "4096"
;  },
;  "MemoryData": {
;    "0x1000000": "0x87654321",
;    "0x1000010": "51 55 55 55 55 55 55 55"
;  }
;}
;%endif

(%1) IRHeader %2, #0
  (%2) CodeBlock %6, %12, %1
    (%6 i0) BeginBlock %2
    %AddrA i64 = Constant #0x1000000
    %MemValueA i32 = LoadMem GPR, #4, %AddrA i64, %Invalid, #4, SXTX, #1
    %Shift i64 = Constant #0x1
    %ResultA i32 = Lshl #4, %MemValueA, %Shift
    %ResultB i64 = Lshl #8, %MemValueA, %Shift
    (%Store i64) StoreRegister %ResultA i64, #0, #0x8, GPR, GPRFixed, #8
    (%Store i64) StoreRegister %ResultB i64, #0, #0x20, GPR, GPRFixed, #8
;  Constant optimisable version
    %ValueB i64 = Constant #0x87654321
    %ResultC i32 = Lshl #4, %ValueB, %Shift
    %ResultD i64 = Lshl #8, %ValueB, %Shift
    (%Store i64) StoreRegister %ResultC i64, #0, #0x10, GPR, GPRFixed, #8
    (%Store i64) StoreRegister %ResultD i64, #0, #0x18, GPR, GPRFixed, #8
    (%7 i0) Break {0.11.0.128}
    (%12 i0) EndBlock %2
